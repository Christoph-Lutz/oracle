#!/usr/bin/env -S bpftrace --no-warnings
/*
 * Purpose:
 *   Show the number of logons per second by counting
 *   the number of calls to function ksucre (Kernel
 *   Service User Create session).
 *
 * Date:
 *   Sep-27 2024
 *
 * Tested on:
 *   Oracle 19.23
 *
 * Usage:
 *   ./ksucre.bt
 *
 * Notes:
 *   ksucre is also invoked by PX processes (and possibly
 *   by different background processes as well), so just
 *   counting the number ksucre calls will not perfectly
 *   reflect 'end user logons'. Therefore, just consider
 *   this a reasonable approximation.
 *
 *   Adjust the ORACLE_HOME path for your environment.
 */

#define KSPSSIDST_ADDR   0x60009628

BEGIN
{
    printf("Tracing ksucre calls ... Hit ^C to stop.\n\n");
}

uprobe:/u01/app/oracle/product/19.0.0.0/dbhome_1923_1/bin/oracle:ksucre
{
    $sid = str(uptr(KSPSSIDST_ADDR));
    @logons[$sid] = count();
}

interval:s:1 
{
    time("**** %H:%M:%S: \n");
    print(@logons); 
    printf("\n");
    clear(@logons);
}

END 
{
    clear(@logons);
}

